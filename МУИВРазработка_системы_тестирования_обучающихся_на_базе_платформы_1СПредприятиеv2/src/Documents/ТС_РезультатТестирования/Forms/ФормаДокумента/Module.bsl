
&НаКлиенте
Процедура УстановитьПодтверждения(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПодтвержденияЗавершение1",ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещения,
		"Установить текущй результат тестирования в качестве подтверждения тестов?",РежимДиалогаВопрос.ДаНет,,,,);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПодтвержденияЗавершение1(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
			ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПодтвержденияЗавершение2",ЭтаФорма,"Записать");
			ПоказатьВопрос(ОписаниеОповещения,
				"Документ не записан. Записать документ?",РежимДиалогаВопрос.ДаНет,,,,);
		Иначе
			УстановитьПодтвержденияЗавершение2(Результат,"");	
		КонецЕсли;		
	КонецЕсли;	
	
КонецПроцедуры	
	
&НаКлиенте
Процедура УстановитьПодтвержденияЗавершение2(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если Параметры = "Записать" Тогда
			Если НЕ ЗаписатьДокументСервер() Тогда
				Возврат;
			КонецЕсли;	
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ТС_Комментарий) Тогда
			ПоказатьПредупреждение(,"Не задан комментарий документа!");
			Возврат;
		КонецЕсли;
	
		ОбработаноЭлементов = СоздатьПодтвержденияСервер();
		Если ОбработаноЭлементов = Неопределено Тогда
			ПоказатьПредупреждение(,"Ошибка!");
		Иначе
			ПоказатьПредупреждение(,"Установлено подтверждение для " + ОбработаноЭлементов + " элементов",,);
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция СоздатьПодтвержденияСервер()
	
	Возврат Документы.ТС_РезультатТестирования.ЗаполнитьПодтвержденияТестов(Объект.Ссылка);
	
КонецФункции

&НаСервере
Функция ЗаписатьДокументСервер()
	
	Попытка
		ТекОбъект = РеквизитФормыВЗначение("Объект");
		ТекОбъект.Записать();
		ЗначениеВРеквизитФормы(ТекОбъект,"Объект");
		Возврат Истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
		
КонецФункции

&НаСервере
Функция ПостроитьСтрокуПравильныхОтветов(ТекТест)
	
	СтрокаОтвет = "";
	Для Каждого Строка ИЗ ТекТест.Ответы Цикл
		Если Строка.Правильный Тогда 
			СтрокаОтвет = ?(СтрДлина(СтрокаОтвет)=0,"",СтрокаОтвет + ", ") + Строка(Строка.НомерСтроки);
		КонецЕсли;
	КонецЦикла;
	Возврат СтрокаОтвет;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПравильностьОтветов(Команда)
	
	Для Каждого Строка ИЗ Объект.Тесты Цикл
		СтрокаПравильных = ПостроитьСтрокуПравильныхОтветов(Строка.Тест);
		Строка.Правильный = СтрокаПравильных = Строка.Ответ;
	КонецЦикла;
	
	//ПоказатьПредупреждение(,"Статус ответов установлен!");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьТест(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Курс) Тогда
		ПоказатьПредупреждение(,"Не выбран курс!");
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПодобратьТекстЗавершение",ЭтаФорма, );
	ПоказатьВводСтроки(Оповещение,,"Введите текст вопроса для поиска",,Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьТекстЗавершение(Строка, Параметры) Экспорт
	
	Если НЕ Строка = Неопределено Тогда
		Результат =  ПодобратьТестНаСервере(Строка);
		Если Результат <> Истина Тогда
			ПоказатьПредупреждение(,Результат);
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры	

&НаСервере
Функция ПодобратьТестНаСервере(СтрокаПоиска)
	
	СтрокаПоиска = СокрЛП(СтрЗаменить(СтрокаПоиска,"""",""""""));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТС_Тесты.Ссылка
	|ИЗ
	|	Справочник.ТС_Тесты КАК ТС_Тесты
	|ГДЕ
	|	ТС_Тесты.Владелец = &Курс
	|	И ТС_Тесты.Вопрос ПОДОБНО ""%" + СтрокаПоиска + "%""";
	
	Запрос.УстановитьПараметр("Курс", Объект.Курс);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "Тест не найдено!";
	КонецЕсли;
	
	ТЗРезультат = РезультатЗапроса.Выгрузить();
	Если ТЗРезультат.Количество() > 1 Тогда
		Возврат "Найдено несколько вариантов. Уточните запрос!";
	КонецЕсли;
	
	НоваяСтрока = Объект.Тесты.Добавить();
	НоваяСтрока.Тест = ТЗРезультат[0].Ссылка;
	
	Возврат Истина;	
	
КонецФункции

&НаКлиенте
Процедура УстановитьСтатусИПодтверждения(Команда)
	
	ЧислоПравильных = 0;
	ЧислоОшибок = 0;
	Для Каждого Строка ИЗ Объект.Тесты Цикл
		СтрокаПравильных = ПостроитьСтрокуПравильныхОтветов(Строка.Тест);
		Если СтрокаПравильных = Строка.Ответ Тогда
			Строка.Правильный = Истина;
			ЧислоПравильных = ЧислоПравильных + 1;
		Иначе
			Строка.Правильный = Ложь;
			ЧислоОшибок = ЧислоОшибок + 1;
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСтатусИПодтвержденияЗавершение",ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещения,
		"Правильных ответов: " + Строка(ЧислоПравильных) + Символы.ПС + 
		"Ошибочных  ответов: " + Строка(ЧислоОшибок) + Символы.ПС + 
		"Процент: " + ?(ЧислоПравильных + ЧислоОшибок = 0,"0 %",Строка(Цел(ЧислоПравильных / (ЧислоПравильных + ЧислоОшибок) *100)) + " %") + Символы.ПС + 
		"-----------" + Символы.ПС + 
		"Установить подтверждения?"
		,РежимДиалогаВопрос.ДаНет,,,,);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусИПодтвержденияЗавершение(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		Если УстановитьСтатусИПодтвержденияНаСервере() Тогда
			ЭтаФорма.Закрыть();
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция УстановитьСтатусИПодтвержденияНаСервере()
	
	Если НЕ ЗаписатьДокументСервер() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ СоздатьПодтвержденияСервер() ТОгда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;	
	
КонецФункции

