
#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьПодтверждения(Тест) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	//Проверим значение параметра
	Если ТипЗнч(Тест) <> Тип("СправочникСсылка.ТС_Тесты") Тогда
		Возврат ТабличныйДокумент;
	КонецЕсли;	
	
	МакетПодтверждения = ПолучитьМакет("МакетПодтверждения");
	
	//Шапка
	Область = МакетПодтверждения.ПолучитьОбласть("Шапка");
	Область.Параметры.Курс = Тест.Владелец;
	Область.Параметры.Тест = Тест;	
	ТабличныйДокумент.Вывести(Область);
	
	//Шапка подтверждения
	ОбластьПодтверждение = МакетПодтверждения.ПолучитьОбласть("Подтверждение");
	ОбластьСсылка = МакетПодтверждения.ПолучитьОбласть("ПодтверждениеСсылка");
	ОбластьКартинка = МакетПодтверждения.ПолучитьОбласть("ПодтверждениеКартинка");
	ОбластьТекст = МакетПодтверждения.ПолучитьОбласть("ПодтверждениеТекст");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТС_ТестыПодтверждение.НомерСтроки КАК НомерПодтверждения,
	|	ТС_ТестыПодтверждение.Описание,
	|	ТС_ТестыПодтверждение.СсылкаПодтверждение КАК Подтвеждение,
	|	ВЫБОР
	|		КОГДА ТС_ТестыПодтверждение.СсылкаПодтверждение ССЫЛКА Документ.ТС_РезультатТестирования
	|			ТОГДА 1
	|		КОГДА ТС_ТестыПодтверждение.СсылкаПодтверждение ССЫЛКА Справочник.ТС_Файлы
	|			ТОГДА 2
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК ТипПодтверждения
	|ИЗ
	|	Справочник.ТС_Тесты.Подтверждение КАК ТС_ТестыПодтверждение
	|ГДЕ
	|	ТС_ТестыПодтверждение.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТС_ТестыПодтверждение.НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Тест);	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ОбластьПодтверждение.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
		ТабличныйДокумент.Вывести(ОбластьПодтверждение);
		Если ВыборкаДетальныеЗаписи.ТипПодтверждения = 1 Тогда
			ОбластьСсылка.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ТабличныйДокумент.Вывести(ОбластьСсылка);
		ИначеЕсли ВыборкаДетальныеЗаписи.ТипПодтверждения = 2 Тогда
			Картинка = Новый Картинка(ВыборкаДетальныеЗаписи.Подтвеждение.Изображение.Получить());
			ОбластьКартинка.Рисунки.Изображение.Картинка = Картинка;
			ТабличныйДокумент.Вывести(ОбластьКартинка);
		Иначе
			ОбластьТекст.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
			ТабличныйДокумент.Вывести(ОбластьТекст);
		КонецЕсли;			
		
	КонецЦикла;

	Возврат ТабличныйДокумент;	
	
КонецФункции

#КонецОбласти

#Область Инициализация

#КонецОбласти