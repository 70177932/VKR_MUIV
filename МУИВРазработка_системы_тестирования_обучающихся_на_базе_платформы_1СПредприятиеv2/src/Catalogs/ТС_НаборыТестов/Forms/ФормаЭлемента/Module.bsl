
#Область ПеременныеФормы

&НаКлиенте
Перем ВладелецДоВыбора;

#КонецОбласти

#Область СобытияФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВладелецДоВыбора = Объект.Владелец;
	
КонецПроцедуры

#КонецОбласти

#Область ДействияФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Если ВладелецДоВыбора <> Объект.Владелец 
		И Объект.Содержание.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВладелецПриИзмененииЗавершение",ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения,
			"При смене владельца табличная часть будет очищена, продолжить?",РежимДиалогаВопрос.ДаНет,,,,);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзмененииЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Объект.Владелец = ВладелецДоВыбора;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Объект.Содержание.Очистить();
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура СодержаниеТестПриИзменении(Элемент)
	
	ТекНомерСтроки = Элементы.Содержание.ТекущиеДанные.НомерСтроки;
	РезультатПроверки = СодержаниеТестПриИзмененииСервер(ТекНомерСтроки);	
	Если РезультатПроверки <> Ложь Тогда
		ПоказатьПредупреждение(,РезультатПроверки);
	КонецЕсли;	
		
КонецПроцедуры

&НаСервере
Функция СодержаниеТестПриИзмененииСервер(ТекНомерСтроки)
	
	РезультатПроверки = Ложь;
	ТекСтрока = Объект.Содержание.Получить(ТекНомерСтроки-1);
	ТекТест = ТекСтрока.Тест;
	Для Каждого Строка ИЗ Объект.Содержание Цикл
		Если Строка.НомерСтроки = ТекНомерСтроки Тогда
			Продолжить;
		КонецЕсли;
		Тест = Строка.Тест;
		Если ТекТест.ЭтоГруппа Тогда
			Если Тест.ЭтоГруппа Тогда
				Если ТекТест = Тест Тогда
					РезультатПроверки = 
					"Группа тестов " + ТекТест + " уже присутствует в данном наборе тестов.";
					Объект.Содержание.Удалить(ТекНомерСтроки-1);
					Прервать;
				КонецЕсли;	
			Иначе
				Если Тест.ПринадлежитЭлементу(ТекТест) Тогда
					РезультатПроверки = 
					"В группу " + ТекТест + " входит тест " + Тест + ", который уже добавлен в данный набор тестов.";
					Объект.Содержание.Удалить(ТекНомерСтроки-1);
					Прервать;
				КонецЕсли;
			КонецЕсли;	
		Иначе	
			Если Тест.ЭтоГруппа Тогда
				Если ТекТест.ПринадлежитЭлементу(Тест) Тогда
					РезультатПроверки = 
					"Тест " + ТекТест + " входит в группу " + Тест + ", которая уже добавлена в данный набор тестов.";
					Объект.Содержание.Удалить(ТекНомерСтроки-1);
					Прервать;
				КонецЕсли;
			ИначеЕсли Тест = ТекТест Тогда
				РезультатПроверки = 
				"Тест " + ТекТест + " уже присутствует в данном наборе тестов.";
				Объект.Содержание.Удалить(ТекНомерСтроки-1);
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

